apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply from: "${rootProject.getRootDir().getAbsolutePath()}/script/testlib.gradle"

def srcDirs = [
        "carousel",
        "video",
        "colorpicker",
        "drawable",
        "animationsExprimental",
        "newLayoutExprimental",
        "scrollExprimental",
        "parallax",

]
android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion
    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        component {
            initWith debug
            debuggable true
        }
    }
    sourceSets {
        main {
            java.excludes = [
                    '**/build/**',
            ]
            srcDirs.forEach {
                assets.srcDirs += "$projectDir/$it/main/assets"
                aidl.srcDirs += "$projectDir/$it/main/aidl"
                res.srcDirs += "$projectDir/$it/main/res-frame-animation"
                res.srcDirs += "$projectDir/$it/main/res"
                java.srcDirs += "$projectDir/$it/main/java"
            }
        }
        androidTest {
            srcDirs.forEach {
                assets.srcDirs += "$projectDir/$it/androidTest/assets"
                aidl.srcDirs += "$projectDir/$it/androidTest/aidl"
                res.srcDirs += "$projectDir/$it/androidTest/res"
                java.srcDirs += "$projectDir/$it/androidTest/java"
            }
        }
        test {
            srcDirs.forEach {
                java.srcDirs += "$projectDir/$it/test/java"
            }

        }
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    api project(path: ':common')
//    implementation project(path: ':image')
    /**
     * ================================ui start=====================================
     */
    api "androidx.appcompat:appcompat:1.0.2"
    api "androidx.recyclerview:recyclerview:1.0.0"

    api 'android.arch.navigation:navigation-fragment:1.0.0'
    api 'android.arch.navigation:navigation-ui:1.0.0'
    api 'android.arch.navigation:navigation-fragment-ktx:1.0.0'
    api 'android.arch.navigation:navigation-ui-ktx:1.0.0'
    //layout
    api 'androidx.constraintlayout:constraintlayout:1.1.3'
    api 'com.google.android:flexbox:1.1.0'
    //layout

    api 'androidx.palette:palette:1.0.0'
    /*
   *该库和`com.android.support:design:`都存在v4，编译时会报错：
   *com.android.build.api.transform.TransformException: java.util.zip.ZipException: duplicate entry: *android/support/v4/app/INotificationSideChannel$Stub$Proxy.class
   *解决方法：需要exclude掉v4包
    */
    api "com.google.android.material:material:1.1.0-alpha06"

    api 'com.airbnb.android:lottie:2.8.0'
    api 'androidx.vectordrawable:vectordrawable:1.1.0-beta01'
    /**
     * ================================ui end=====================================
     */


}
